Long Umber Copperhead

High

# Invalid vest unlock flow rate calculations in `FluidLocker::_vestUnlock(...)` leads to recepients paying much higher tax rates than intended

### Summary

The `FluidLocker` contract allows users to lock `FLUID` tokens, and then at specific `unlockPeriods` to unlock them. When unlocking, users can choose to do it either instantly, and pay an 80% tax fee to stakers, or use a `vesting` scheme, where funds are  `flown` to the user, using a `Fontaine`, based on the unlock period chosen. However, with the current logic in `FluidLocker::_vestUnlock(...)`, there is no difference if a recipient unlocks with `unlockPeriod = 0` or `unlockPeriod = 540 days` as there will be a constant 80% tax.

### Root Cause

In [FluidLocker::_getUnlockingPercentage(...)](https://github.com/sherlock-audit/2024-11-superfluid-locking-contract/blob/main/fluid/packages/contracts/src/FluidLocker.sol#L384) invalid calculations are being carried out, leading to the function always producing a result of `2000`, no matter what `unlockPeriod` is given. Because of this, the resulting `unlockFlowRate` will always be capped at `20%` meaning that recipients who unlock at the max period will pay a constant tax of `80%` to stakers, as if they unlocked at the very beginning.

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

1. Locker A's owner unlocks tokens for Alice with an unlock period of `0`.
2. Alice pays `80%` tax rate to instantly receive her tokens.
3. Locker A's owner unlocks tokens for Bob with an `unlockPeriod` of `540 days`.
4. Bob should pay `0 %` tax, however due to improper calculations, he ends up paying `80%` tax as well.,

### Impact

`FluidLocker::unlock(...)` recipients who choose to unlock using the vesting option pay higher taxes than intended, thus receiving fewer tokens than what they should be.

### PoC

The following test can be added in `FluidLocker.t.sol` and ran with `forge test --mt test_getUnlockingPercentage -vv`

```solidity
uint256 private constant _SCALER = 1e18;

uint256 private constant _PERCENT_TO_BP = 100;

function _getUnlockingPercentage(uint128 unlockPeriod) internal pure returns (uint256 unlockingPercentageBP) {
    unlockingPercentageBP = (
        _PERCENT_TO_BP
            * (
                ((80 * _SCALER) / Math.sqrt(540 * _SCALER)) * (Math.sqrt(unlockPeriod * _SCALER) / _SCALER)
                    + 20 * _SCALER
            )
    ) / _SCALER;
}

// Add this test function to your FluidLockerTest contract
function test_getUnlockingPercentage() public {
    // Create a helper function to access the internal function

    // Test different periods
    uint128 sevenDays = 7 days;
    uint128 thirtyDays = 30 days;
    uint128 ninetyDays = 90 days;
    uint128 oneEightyDays = 180 days;
    uint128 fiveFortyDays = 540 days;

    uint256 sevenDaysPercent = _getUnlockingPercentage(sevenDays);
    uint256 thirtyDaysPercent = _getUnlockingPercentage(thirtyDays);
    uint256 ninetyDaysPercent = _getUnlockingPercentage(ninetyDays);
    uint256 oneEightyDaysPercent = _getUnlockingPercentage(oneEightyDays);
    uint256 fiveFortyDaysPercent = _getUnlockingPercentage(fiveFortyDays);

    console.log("7 days unlock percentage:", sevenDaysPercent);
    console.log("30 days unlock percentage:", thirtyDaysPercent);
    console.log("90 days unlock percentage:", ninetyDaysPercent);
    console.log("180 days unlock percentage:", oneEightyDaysPercent);
    console.log("540 days unlock percentage:", fiveFortyDaysPercent);
}
```
And the output is:

```bash
Ran 1 test for test/FluidLocker.t.sol:FluidLockerTest
[PASS] test_getUnlockingPercentage() (gas: 19376)
Logs:
  7 days unlock percentage: 2000
  30 days unlock percentage: 2000
  90 days unlock percentage: 2000
  180 days unlock percentage: 2000
  540 days unlock percentage: 2000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 11.22ms (886.54Âµs CPU time)

Ran 1 test suite in 159.53ms (11.22ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

Divide the current `_getUnlockingPercentage` calculations into batches to ensure proper truncation:

```diff
index f31ad92..f39a89a 100644
--- a/fluid/packages/contracts/src/FluidLocker.sol
+++ b/fluid/packages/contracts/src/FluidLocker.sol
@@ -382,13 +382,14 @@ contract FluidLocker is Initializable, ReentrancyGuard, IFluidLocker {
     }
 
     function _getUnlockingPercentage(uint128 unlockPeriod) internal pure returns (uint256 unlockingPercentageBP) {
-        unlockingPercentageBP = (
-            _PERCENT_TO_BP
-                * (
-                    ((80 * _SCALER) / Math.sqrt(540 * _SCALER)) * (Math.sqrt(unlockPeriod * _SCALER) / _SCALER)
-                        + 20 * _SCALER
-                )
-        ) / _SCALER;
+        // First calculate sqrt(unlockPeriod/_MAX_UNLOCK_PERIOD)
+        uint256 periodRatio = Math.sqrt((unlockPeriod * _SCALER) / _MAX_UNLOCK_PERIOD);
+
+        // Scale it to the range [20, 100]
+        uint256 percentage = 20 + ((80 * periodRatio) / Math.sqrt(_SCALER));
+
+        // Convert to basis points (multiply by 100)
+        unlockingPercentageBP = percentage * 100;
     }
```

With the above change, the test output looks like this:

```bash
Ran 1 test for test/FluidLocker.t.sol:FluidLockerTest
[PASS] test_getUnlockingPercentage() (gas: 18470)
Logs:
  7 days unlock percentage: 2900
  30 days unlock percentage: 3800
  90 days unlock percentage: 5200
  180 days unlock percentage: 6600
  540 days unlock percentage: 10000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 11.90ms (1.07ms CPU time)

Ran 1 test suite in 160.39ms (11.90ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```