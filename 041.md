Huge Grey Butterfly

High

# Vest Unlock Flow Rate and Tax Rate are Wrongly Computed

### Summary

The vest unlock flow rate is locked at 20% of the calculated `globalFlowRate` regardless of the passed `unlockPeriod` as opposed to being dynamic to accommodate shorter/longer unlock periods. This in turn causes inaccurate flow distributions to both tax and recipient.

### Root Cause

When a locker owner calls `FluidLocker::unlock()` - they have the option to either go for an instant or vesting unlock. This unlock period is checked to be between `7 - 540 days`, depending on the duration - the unlocking percentage rate is calculated dynamically (i.e not static).

Due to the way the math is implemented in [FluidLocker::_getUnlockingPercentage()](https://github.com/sherlock-audit/2024-11-superfluid-locking-contract/blob/1fa5f86024be5f269e1a0898b1f939f1d4cce149/fluid/packages/contracts/src/FluidLocker.sol#L384-L392), this value will always return a static value of `2000 (i.e 20%)` regardless of the actual duration.

```solidity
    function _getUnlockingPercentage(uint128 unlockPeriod) internal pure returns (uint256 unlockingPercentageBP) { // @audit: will always resolve to 2000 regardless of unlockPeriod
        unlockingPercentageBP = (
            _PERCENT_TO_BP
                * (
                    ((80 * _SCALER) / Math.sqrt(540 * _SCALER)) * (Math.sqrt(unlockPeriod * _SCALER) / _SCALER)
                        + 20 * _SCALER
                )
        ) / _SCALER;
```

This in turn further affects previous function calls that depend on the accuracy of this value e.g

[FluidLocker::_calculateVestUnlockFlowRates](https://github.com/sherlock-audit/2024-11-superfluid-locking-contract/blob/1fa5f86024be5f269e1a0898b1f939f1d4cce149/fluid/packages/contracts/src/FluidLocker.sol#L372-L382)

```solidity
    function _calculateVestUnlockFlowRates(uint256 amountToUnlock, uint128 unlockPeriod)
        internal
        pure
        returns (int96 unlockFlowRate, int96 taxFlowRate)
    {
        int96 globalFlowRate = int256(amountToUnlock / unlockPeriod).toInt96();

        unlockFlowRate = (globalFlowRate * int256(_getUnlockingPercentage(unlockPeriod))).toInt96()
            / int256(_BP_DENOMINATOR).toInt96();
        taxFlowRate = globalFlowRate - unlockFlowRate;
    }
```

[FluidLocker::_vestUnlock()](https://github.com/sherlock-audit/2024-11-superfluid-locking-contract/blob/1fa5f86024be5f269e1a0898b1f939f1d4cce149/fluid/packages/contracts/src/FluidLocker.sol#L349-L370)

```solidity
    function _vestUnlock(uint256 amountToUnlock, uint128 unlockPeriod, address recipient) internal { 
        // Calculate the unlock and penalty flow rates based on requested amount and unlock period
        (int96 unlockFlowRate, int96 taxFlowRate) = _calculateVestUnlockFlowRates(amountToUnlock, unlockPeriod);

        // Use create2 to deploy a Fontaine Beacon Proxy
        // The salt used for deployment is the hashed encoded Locker address and unlock identifier
        address newFontaine = address(
            new BeaconProxy{ salt: keccak256(abi.encode(address(this), fontaineCount)) }(address(FONTAINE_BEACON), "")
        );

        // Transfer the total amount to unlock to the newly created Fontaine
        FLUID.transfer(newFontaine, amountToUnlock);

        // Persist the fontaine address and increment fontaine counter
        fontaines[fontaineCount] = IFontaine(newFontaine);
        fontaineCount++;

        // Initialize the new Fontaine instance (this initiate the unlock process)
        IFontaine(newFontaine).initialize(recipient, unlockFlowRate, taxFlowRate);

        emit FluidUnlocked(unlockPeriod, amountToUnlock, recipient, newFontaine);
    }
```

### Impact

Inaccurate flow rates distribution:

1. Wrong `taxFlowRate` distribution from created `Fountaine` to `TAX_DISTRIBUTION_POOL`.
2. Wrong `unlockFlowRate` to recipient.
3. Recipient is permanently locked in at 20% and tax at 80%, regardless of `unlockPeriod`.

#3 happens because a new `Fountaine` is deployed and initialized with 2 wrong values:

```solidity
        // Initialize the new Fontaine instance (this initiate the unlock process)
        IFontaine(newFontaine).initialize(recipient, unlockFlowRate, taxFlowRate);
```

Here, `unlockFlowRate` will always be 20% and is for the user, while `taxFlowRate` will always yield 80% which is for the `TAX_DISTRIBUTION_POOL`, depending on the amount and supplied period - these values will always overestimate or underestimate the distribution rates.

Thus, the recipient or tax pool will always receive less/more than either party should.

### PoC

Running the following with either the min or max periods of `7 and 540 days`, or anything in between will always yield `2000 (representing 20%)`

```solidity
    function _getUnlockingPercentage(uint128 unlockPeriod) internal pure returns (uint256 unlockingPercentageBP) {
        uint256 _PERCENT_TO_BP = 100;
        uint256 _SCALER = 1e18;

        unlockingPercentageBP = (
            _PERCENT_TO_BP
                * (
                    ((80 * _SCALER) / Math.sqrt(540 * _SCALER)) * (Math.sqrt(unlockPeriod * _SCALER) / _SCALER)
                        + 20 * _SCALER
                )
        ) / _SCALER;
    }
```

This happens because `Math.sqrt(unlockPeriod * _SCALER)` will always yield a value smaller than `_SCALER` even in it's highest value of `unlockPeriod = 540 days`. Further dividing by `_SCALER` will always yield 0, thus, the output of `2000` remains constant through any and all iterations.