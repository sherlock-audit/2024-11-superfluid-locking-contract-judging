Damp Sapphire Goblin

High

# FluidLocker::_calculateVestUnlockFlowRates() returns incorrect unlockFlowRate and taxFlowRate

## Summary
Performing division before multiplication, the wrong usage of "scalers" and using numerical literals instead of time units lead to significantly incorrect results.

## Vulnerability Detail
The issue originates in the _getUnlockingPercentage function:
```solidity
File: FluidLocker.sol
384:    function _getUnlockingPercentage(uint128 unlockPeriod) internal pure returns (uint256 unlockingPercentageBP) {
385:        unlockingPercentageBP = (
386:            _PERCENT_TO_BP
387:                * (
388:                    ((80 * _SCALER) / Math.sqrt(540 * _SCALER)) * (Math.sqrt(unlockPeriod * _SCALER) / _SCALER)
389:                        + 20 * _SCALER
390:                )
391:        ) / _SCALER;
392:    }
```

1. First, let us examine the last part of the multiplication: `Math.sqrt(unlockPeriod * _SCALER) / _SCALER`. As observed in the `unlock()` function, the `unlockPeriod` value is constrained between the `_MIN_UNLOCK_PERIOD` and `_MAX_UNLOCK_PERIOD` constants, which correspond to `7 days` and `540 days`, respectively:

```solidity
File: FluidLocker.sol
205:    function unlock(uint128 unlockPeriod, address recipient) external nonReentrant onlyLockerOwner unlockAvailable {
206:        // Enforce unlock period validity
207:        if (unlockPeriod != 0 && (unlockPeriod < _MIN_UNLOCK_PERIOD || unlockPeriod > _MAX_UNLOCK_PERIOD)) {
208:            revert INVALID_UNLOCK_PERIOD();
209:        }
210:    ...
```

At the same time, the `_SCALER` constant is `1e18`. Let's calculate what this results in for the most significant value of `unlockPeriod`, which is `540` days, or `46,656,000` seconds:

```solidity
Math.sqrt(unlockPeriod * _SCALER) / _SCALER = Math.sqrt(46,656,000 * 1e18) / 1e18 = ~0.0000068305197
```
This value will effectively be truncated to `0`, leaving the `_getUnlockingPercentage` function to always return 20 (percent).

2. The second issue in the formula stems from the fact that it uses `540`, while the expectation is for it to be equal to `540 days`, or simply `46,656,000` seconds. Such discrepancy may result in the function output being a much higher value that the maximum expected 100 percent.

## Impact
* Users always lose 80% of their funds to the tax pool.
* If only the first part is fixed, `unlock()` will revert when the unlock interval is non-zero.

## Code Snippet
https://github.com/sherlock-audit/2024-11-superfluid-locking-contract/blob/main/fluid/packages/contracts/src/FluidLocker.sol#L385-L391

## Tools Used
Manual review

## Recommendation
* Use `540 days` instead `540`;
* Multiply before division;
* Limit usage of division as much as possible.

```solidity
    function _getUnlockingPercentage(uint128 unlockPeriod) internal pure returns (uint256 unlockingPercentageBP) {
        unlockingPercentageBP =
            80 * _PERCENT_TO_BP  * Math.sqrt(unlockPeriod * _SCALER) / Math.sqrt(540 days * _SCALER) +  20 * _PERCENT_TO_BP;
    }
```