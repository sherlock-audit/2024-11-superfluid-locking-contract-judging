Rapid Wool Owl

High

# [High] Flow Rate Overwrite in `startFunding` Leads to Locked `FLUID` Tokens and Disrupted Fund Distribution

### Summary

The `startFunding` function in the `FluidEPProgramContract.sol` contract fails to account for existing flow rates when funding a program’s distribution pool, leading to the overwrite of previously allocated flow rates. This results in `FLUID` tokens associated with earlier fundings becoming locked within the contract and halts their intended distribution to the program. While the flow rate for the `TAX_DISTRIBUTION_POOL` is incremented correctly, the program's distribution pool experiences a reset, disrupting the continuity of fund distribution. 

### Root Cause

In the FluidEPProgramContract.sol contract:

- The distributeFlow function for the program's distribution pool overwrites the existing flow rate with the newly calculated fundingFlowRate, ignoring any prior flow rate.

  https://github.com/sherlock-audit/2024-11-superfluid-locking-contract/blob/main/fluid/packages/contracts/src/FluidEPProgramManager.sol#L272-L273

- Conversely, the flow rate for the TAX_DISTRIBUTION_POOL is incremented correctly by adding the current flow rate to the new calculated rate:
  https://github.com/sherlock-audit/2024-11-superfluid-locking-contract/blob/main/fluid/packages/contracts/src/FluidEPProgramManager.sol#L426-L433


### Internal pre-conditions

1. The owner must call the `startFunding` function to initiate funding for a program.
2. The program's distribution pool must already have an existing flow rate from prior funding (to be overwritten).
3. The `fundingFlowRate` must be calculated and passed to the `distributeFlow` function for the program's distribution pool.

### External pre-conditions

1. The `FLUID` Treasury must have sufficient tokens approved for transfer to the contract to execute the funding.
2. The `distributeFlow` function for the program's distribution pool must not include logic to account for existing flow rates, allowing the overwrite to occur.
3. There must be no external mechanism to detect or prevent the overwriting of flow rates for a previously funded program.

### Attack Path

1. The owner calls the `startFunding` function to initiate funding for a program.
2. The `distributeFlow` function for the program's distribution pool overwrites the current flow rate with the newly calculated `fundingFlowRate`.
3. `FLUID` tokens associated with prior flow rates remain locked in the contract, and the distribution of these funds halts.

### Impact

The issue leads to `FLUID` tokens becoming locked within the contract, rendering them inaccessible for redistribution. When the `startFunding` function is invoked for a program with an existing distribution pool, the flow rate of the pool is overwritten instead of being updated incrementally. This results in funds associated with prior flow rates no longer being distributed. These locked tokens cannot be retrieved or utilized by the program, effectively stalling the intended flow of funds.

This locking of tokens disrupts the distribution mechanism, causing previous fundings to become irretrievable and halting ongoing financial support to the program’s recipients. Over time, the accumulation of locked tokens significantly diminishes the protocol's efficiency and flexibility. Users and stakeholders relying on consistent fund flows experience disruptions, leading to a potential loss of trust in the protocol. 

### PoC

```solidity

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.23;

import { SFTest } from "./SFTest.t.sol";
import { ECDSA } from "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import { SafeCast } from "@openzeppelin-v5/contracts/utils/math/SafeCast.sol";

import { Script, console2 } from "forge-std/Script.sol";

import {
    ISuperToken,
    ISuperfluidPool
} from "@superfluid-finance/ethereum-contracts/contracts/interfaces/superfluid/ISuperfluid.sol";
import { SuperTokenV1Library } from "@superfluid-finance/ethereum-contracts/contracts/apps/SuperTokenV1Library.sol";

import { EPProgramManager, IEPProgramManager } from "../src/EPProgramManager.sol";
import { FluidEPProgramManager } from "../src/FluidEPProgramManager.sol";
import { IFluidLocker } from "../src/interfaces/IFluidLocker.sol";

using SuperTokenV1Library for ISuperToken;
using ECDSA for bytes32;
using SafeCast for int256;


contract FluidEPProgramManagerTest is SFTest {
    IFluidLocker public aliceLocker;
    IFluidLocker public bobLocker;

    function testStartFundingWithSubsidy(uint256 _programId, uint256 _fundingAmount, uint96 _subsidyRate) external {
        vm.assume(_programId > 0);
        _fundingAmount = bound(_fundingAmount, 100_000e18, 100_000_000e18);

        // Subsidy rate fuzzed between 1% and 99%
        _subsidyRate = uint96(bound(_subsidyRate, 100, 9_900));

        uint96 signerPkey = 69_420;

        vm.prank(ADMIN);
        _programManager.setSubsidyRate(_subsidyRate);

        ISuperfluidPool pool = _helperCreateProgram(_programId, ADMIN, vm.addr(signerPkey));
        _helperGrantUnitsToAlice(_programId, 1, signerPkey);
        _helperBobStaking();

        vm.prank(FLUID_TREASURY);
        _fluid.approve(address(_programManager), _fundingAmount);

        _fundingAmount = _fundingAmount - 100_000e18;

        vm.prank(ADMIN);
        _programManager.startFunding(_programId, _fundingAmount);

        uint256 subsidyAmount = (_fundingAmount * _subsidyRate) / 10_000;
        uint256 fundingAmount = _fundingAmount - subsidyAmount;

        // Calculate the funding and subsidy flow rates
        int96 requestedSubsidyFlowRate = int256(subsidyAmount / PROGRAM_DURATION).toInt96();
        int96 requestedProgramFlowRate = int256(fundingAmount / PROGRAM_DURATION).toInt96();

        (, int96 totalProgramDistributionFlowRate) =
            _fluid.estimateFlowDistributionActualFlowRate(address(_programManager), pool, requestedProgramFlowRate);

        (, int96 totalSubsidyDistributionFlowRate) = _fluid.estimateFlowDistributionActualFlowRate(
            address(_programManager), _programManager.TAX_DISTRIBUTION_POOL(), requestedSubsidyFlowRate
        );

        assertEq(
            pool.getMemberFlowRate(address(aliceLocker)),
            totalProgramDistributionFlowRate,
            "program distribution flow rate is incorrect"
        );

        assertEq(
            _stakingRewardController.taxDistributionPool().getMemberFlowRate(address(bobLocker)),
            totalSubsidyDistributionFlowRate,
            "subsidy distribution flow to staker is incorrect"
        );

        // second funding
        uint256 second_funding_rate = 100_000e18;
        vm.prank(ADMIN);
        _programManager.startFunding(_programId, second_funding_rate);

        subsidyAmount = (second_funding_rate * _subsidyRate) / 10_000;
        fundingAmount = second_funding_rate - subsidyAmount;

        // Calculate the funding and subsidy flow rates
        requestedSubsidyFlowRate = int256(subsidyAmount / PROGRAM_DURATION).toInt96();
        requestedProgramFlowRate = int256(fundingAmount / PROGRAM_DURATION).toInt96();

        (, int96 totalProgramDistributionFlowRate2) =
            _fluid.estimateFlowDistributionActualFlowRate(address(_programManager), pool, requestedProgramFlowRate);

        (, int96 totalSubsidyDistributionFlowRate2) = _fluid.estimateFlowDistributionActualFlowRate(
            address(_programManager), _programManager.TAX_DISTRIBUTION_POOL(), requestedSubsidyFlowRate
        );

        assertEq(
            _stakingRewardController.taxDistributionPool().getMemberFlowRate(address(bobLocker)),
            totalSubsidyDistributionFlowRate + totalSubsidyDistributionFlowRate2,
            "subsidy distribution flow to staker is incorrect"
        );

        // proves that flow rate is overwritten instead of increment
        assertEq(
            pool.getMemberFlowRate(address(aliceLocker)),
            totalProgramDistributionFlowRate2
        );
    }
    
    function _helperGrantUnitsToAlice(uint256 programId, uint256 units, uint96 signerPkey) internal {
        uint256 nonce = _programManager.getNextValidNonce(programId, ALICE);
        bytes memory validSignature = _helperGenerateSignature(signerPkey, ALICE, units, programId, nonce);

        vm.prank(ALICE);
        _programManager.updateUnits(programId, units, nonce, validSignature);
    }

    function _helperBobStaking() internal {
        _helperFundLocker(address(bobLocker), 10_000e18);
        vm.prank(BOB);
        bobLocker.stake();
    }

    function _helperStartFunding(uint256 _programId, uint256 _fundingAmount) internal {
        vm.prank(FLUID_TREASURY);
        _fluid.approve(address(_programManager), _fundingAmount);

        vm.prank(ADMIN);
        _programManager.startFunding(_programId, _fundingAmount);
    }
}

```

### Mitigation

To mitigate the issue:

1. Update the logic for the program's distribution pool to correctly add the new fundingFlowRate to the existing flow rate.
    ```solidity
    int96 currentFundingFlowRate = token.getFlowDistributionFlowRate(address(this), program.distributionPool);
    int96 updatedFundingFlowRate = currentFundingFlowRate + fundingFlowRate;
    token.distributeFlow(address(this), program.distributionPool, updatedFundingFlowRate);
    ```
2. Add unit tests to verify that repeated calls to `startFunding` incrementally adjust the flow rate without overwriting prior allocations.
3. Implement a mechanism to allow recovery or reallocation of locked funds in case of errors.